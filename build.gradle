/**
 * Java Parallel For (JParFor).
 */
plugins {
    id 'org.ajoberstar.grgit' version '1.4.2'
    id 'org.ajoberstar.github-pages' version '1.4.2'
    //id 'org.ajoberstar.release-opinion' version '1.4.2'
    //id 'org.ajoberstar.release-base' version '1.4.2'
}

/**
 * See:
 *      https://docs.gradle.org/current/userguide/java_plugin.html
 */
apply plugin: 'java'

/**
 * See:
 *      https://docs.gradle.org/current/userguide/eclipse_plugin.html
 */
apply plugin: 'eclipse'

/**
 * See:
 *      https://github.com/ajoberstar/gradle-git/
 */
apply plugin: 'org.ajoberstar.github-pages'

/**
 * See:
 *      https://stackoverflow.com/questions/17262856/
 */
group = theGroup
version = theVersion
sourceCompatibility = theSourceCompatibility

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
}

/**
 * See:
 *      http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
 *      https://stackoverflow.com/questions/22528767/
 */
if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        // Disable the crazy super-strict doclint tool in Java 8.
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

/**
 * See:
 *      https://github.com/ajoberstar/gradle-git/wiki/org.ajoberstar.github-pages/
 */
githubPages {
    repoUri = 'https://github.com/dzavodnikov/JParFor/'
    pages {
        from javadoc
    }
}

/**
 * See:
 *      https://stackoverflow.com/questions/11474729/
 */
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
